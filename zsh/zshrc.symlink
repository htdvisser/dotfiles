export DOTFILES="$HOME/dotfiles"
export PROJECTS="$HOME/projects"

fpath=("$DOTFILES/zsh/plugins/zsh-completions/src" $fpath)

load_plugin() {
  source "$DOTFILES/zsh/plugins/$1/$1.zsh"
}

load_plugin zsh-autosuggestions
load_plugin zsh-syntax-highlighting
load_plugin zsh-history-substring-search

unset load_plugin

if command -v direnv >/dev/null 2>&1; then
  eval "$(direnv hook zsh)"
fi

typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# Load Paths

if [ -x /usr/libexec/path_helper ]; then
  eval $(PATH="" /usr/libexec/path_helper -s)
fi

for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done
export PATH="$HOME/bin:$DOTFILES/bin:$PATH"

# Load Functions
fpath=($DOTFILES/functions $fpath)
autoload -U "$DOTFILES"/functions/*(:t)

# Load Other ZSH Files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# Completion
autoload -Uz compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' menu select=2
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit
else
  compinit -C
fi
unset updated_at

for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# Local Configuration
if [ -f "$HOME/.localrc" ]; then
  source "$HOME/.localrc"
fi

unset command_exists
unset DOTFILES
